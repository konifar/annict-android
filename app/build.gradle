import com.android.ddmlib.DdmPreferences

apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.vanniktech.android.apk.size'

// Manifest version
def versionMajor = 0
def versionMinor = 1
def versionPatch = 0

// CI
def isCi = "true".equals(System.getenv("CI"))
def preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))

// Api keys
def annictApplicationId = System.getenv("ANNICT_APPLICATION_ID")
def annictSecretKey = System.getenv("ANNICT_SECRET_KEY")
def api_keys_gradle = './../api_keys.gradle'

if (annictApplicationId == null || annictSecretKey == null) {
    println "Env [ANNICT_APPLICATION_ID] & [ANNICT_SECRET_KEY] are not set to env variables."
    if (file(api_keys_gradle).exists()) {
        apply from: api_keys_gradle
        annictApplicationId = annict_application_id
        annictSecretKey = annict_secret_key
    } else {
        throw new GradleException("${api_keys_gradle} does not exist. Please run ./script/prepare.sh.")
    }
}

def packageName = "com.konifar.annict"

android {
    compileSdkVersion compile_sdk_version as int
    buildToolsVersion build_tools_version
    dataBinding {
        enabled = true
    }

    dexOptions {
        // Skip pre-dexing when running on CI or when disabled via -Dpre-dex=false.
        preDexLibraries = preDexEnabled && !isCi
    }

    defaultConfig {
        applicationId packageName
        minSdkVersion min_sdk_version as int
        targetSdkVersion target_sdk_version as int
        versionCode versionMajor * 100 + versionMinor * 10 + versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // Referred to https://github.com/circleci/EspressoSample to make the Espresso tests work on CircleCI.
        DdmPreferences.setTimeOut(60000)

        vectorDrawables.useSupportLibrary = true

        buildConfigField("String", "ANNICT_APPLICATION_ID", "\"${annictApplicationId}\"")
        buildConfigField("String", "ANNICT_SECRET_KEY", "\"${annictSecretKey}\"")
    }

    packagingOptions {
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    signingConfigs {
        debug {
            storeFile rootProject.file("debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
        release {
            storeFile rootProject.file("release.keystore")
            storePassword System.getenv("RELEASE_STORE_PASSWORD")
            keyAlias System.getenv("RELEASE_KEY_ALIAS")
            keyPassword System.getenv("RELEASE_KEY_PASSWORD")
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            applicationIdSuffix '.debug'
            versionNameSuffix "-debug"
            shrinkResources true
            debuggable true
            zipAlignEnabled true
        }
        release {
            signingConfig signingConfigs.release
            shrinkResources true
            debuggable false
            zipAlignEnabled true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        develop {
            applicationId "${packageName}.develop"
        }
        production {
            applicationId packageName
        }
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    aaptOptions {
        noCompress 'apk'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'LICENSE.txt'
    }
}

dependencies {
    // Support Library
    compile "com.android.support:support-v4:${support_lib_version}"
    compile "com.android.support:appcompat-v7:${support_lib_version}"
    compile "com.android.support:support-annotations:${support_lib_version}"
    compile "com.android.support:design:${support_lib_version}"
    compile "com.android.support:recyclerview-v7:${support_lib_version}"
    compile "com.android.support:customtabs:${support_lib_version}"
    compile "com.android.support:support-vector-drawable:${support_lib_version}"
    compile "com.android.support:animated-vector-drawable:${support_lib_version}"

    // Network
    compile 'com.squareup.retrofit2:retrofit:2.0.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.0.0'
    compile 'com.squareup.retrofit2:converter-gson:2.0.0'
    compile 'com.google.code.gson:gson:2.6.2'
    compile 'com.squareup.okhttp3:okhttp:3.2.0'

    // Android Utility
    compile 'org.parceler:parceler-api:1.0.4'
    apt "org.parceler:parceler:1.0.4"
    debugCompile 'com.facebook.stetho:stetho:1.3.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
    apt 'com.github.gfx.android.orma:orma-processor:2.3.2'
    compile 'com.github.gfx.android.orma:orma:2.3.2'
    compile 'com.rejasupotaro:kvs-schema:5.0.0'
    apt 'com.rejasupotaro:kvs-schema-compiler:5.0.0'

    // Structure
    compile 'com.google.dagger:dagger:2.2'
    apt 'com.google.dagger:dagger-compiler:2.2'
    provided 'javax.annotation:jsr250-api:1.0'
    compile 'io.reactivex:rxjava:1.1.5'
    compile 'io.reactivex:rxandroid:1.2.0'
    compile 'com.annimon:stream:1.0.9'
    retrolambdaConfig 'net.orfjackal.retrolambda:retrolambda:2.3.0'

    // Debug
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.4-beta2'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'

    // UI
    compile('com.github.ozodrukh:CircularReveal:1.3.1@aar') {
        transitive = true;
    }
    compile 'com.booking:rtlviewpager:1.0.1'

    // Test
    testCompile 'junit:junit:4.12'
    testCompile "org.robolectric:robolectric:3.1.1"
    testCompile('com.squareup.assertj:assertj-android:1.1.1') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    testCompile 'org.mockito:mockito-all:1.10.19'
    androidTestCompile('com.android.support.test:runner:0.4.1') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile('com.android.support.test:rules:0.4.1') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile('com.squareup.assertj:assertj-android:1.1.1') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.1') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile('com.android.support.test.espresso:espresso-intents:2.2.1') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    testCompile 'junit:junit:4.12'
    testCompile 'junit:junit:4.12'
}
